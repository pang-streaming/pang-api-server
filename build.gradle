plugins {
    id 'org.springframework.boot' version '3.5.7' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.9.25' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25' apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25' apply false
}

allprojects {
    group = 'com.pang'
    version = '0.0.1-SNAPSHOT'
    description = 'pang-api-server'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    def isKotlin = project.findProperty('moduleLanguage') == 'kotlin'

    if (isKotlin) {
        apply plugin: 'org.jetbrains.kotlin.jvm'
        apply plugin: 'org.jetbrains.kotlin.plugin.spring'
        apply plugin: 'org.jetbrains.kotlin.plugin.jpa'

        kotlin {
            compilerOptions {
                freeCompilerArgs.add('-Xjsr305=strict')
            }
            jvmToolchain(17)
        }

        dependencies {
            implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
            implementation 'org.jetbrains.kotlin:kotlin-reflect'
            implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        }
    } else {
        apply plugin: 'java'

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }

        dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
        }
    }
    tasks.register("prepareKotlinBuildScriptModel"){}

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}